plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'com.github.triplet.play' version '2.5.0'
}

apply from: "${project.rootDir}/buildsystem/android-defaults.gradle"

android {
    defaultConfig {
        def buildNumber = getBuildNumber()

        applicationId "uk.co.alt236.bluetoothconnectionlog"
        versionCode buildNumber
        versionName "1.0.0." + buildNumber

        buildConfigField "String", "GIT_HASH_SHORT", quoteString(execGitHashShort())
        buildConfigField "Boolean", "INSERT_DUMMY_DATA", "false"
    }

    signingConfigs {
        release {
            storeFile file(System.getenv("ANDROID_KEYSTORE") ?: "[KEY_NOT_DEFINED]")
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }

        debug {
            storeFile file("${project.rootDir}/buildsystem/signing_keys/debug.keystore")
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storePassword 'android'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if(isRunningOnCi()) {
                signingConfig signingConfigs.release
            }
        }

        debug {
            applicationIdSuffix ".debug"
            buildConfigField "Boolean", "INSERT_DUMMY_DATA", "true"
            signingConfig signingConfigs.debug
        }
    }
}

dependencies {
    //implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(path: ':database')
    implementation project(path: ':btdeviceinfo')
    implementation project(path: ':sharedprefs')
    implementation project(path: ':map')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation "androidx.appcompat:appcompat:$androidx_appcompat_version"
    implementation "androidx.core:core-ktx:$androidx_core_ktx"
    implementation "androidx.legacy:legacy-support-v4:$androidx_legacy_v4"
    implementation "androidx.recyclerview:recyclerview:$androidx_recyclerview"
    implementation "androidx.lifecycle:lifecycle-extensions:$androidx_lifecycle_version"
    implementation "androidx.preference:preference:$androidx_preferences"
    implementation "androidx.constraintlayout:constraintlayout:$androidx_contraint_layout_version"
    implementation "androidx.constraintlayout:constraintlayout-solver:$androidx_contraint_layout_version"

    implementation "com.google.android.gms:play-services-location:$google_play_services_version"
    implementation "com.google.android.material:material:$google_material_lib_version"

    implementation "io.github.dreierf:material-intro-screen:0.0.6"

    testImplementation "junit:junit:$junit_version"
    androidTestImplementation "androidx.test:runner:$androidx_test_runner_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidx_espresso_core_version"
}

play {
    def credentialsPath = System.getenv("GPLAY_DEPLOY_KEY") ?: "[KEY_NOT_DEFINED]"
    def lastCommitMessage = getLastGitCommitMessage().take(50)
    logger.warn("GPP Config: $credentialsPath")
    logger.warn("Release Name: '$lastCommitMessage'")

    if(isRunningOnCi()) {
        enabled = true
        track = "internal"
        //userFraction = 1.0
        releaseStatus = "completed"
        serviceAccountCredentials = file(credentialsPath)
        releaseName = lastCommitMessage
        artifactDir = file("${project.rootDir}/app/build/outputs/apk/release/")
    } else {
        enabled = false
    }
}
